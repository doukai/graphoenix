buildscript {
    repositories {
        mavenLocal()
        jcenter()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'io.graphoenix:graphoenix-gradle-plugin:0.0.1-SNAPSHOT'
    }
}

plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.1'
}
apply plugin: 'maven'
apply plugin: 'io.graphoenix'

protobuf {
    protoc {
        // artifact = "com.google.protobuf:protoc:3.21.7"
        // for apple m1, please add protoc_platform=osx-x86_64 in $HOME/.gradle/gradle.properties
        if (project.hasProperty('protoc_platform')) {
            artifact = "com.google.protobuf:protoc:3.21.7:${protoc_platform}"
        } else {
            artifact = "com.google.protobuf:protoc:3.21.7"
        }
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.52.1'
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:1.2.3"
        }
        doc {
            artifact = "io.github.pseudomuto:protoc-gen-doc:1.5.1"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            reactor {}
            doc {
                option 'markdown,grpc-docs.md'
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java'
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/reactor'
        }
//        resources {
//            srcDirs 'build/generated/source/proto/main/doc'
//        }
    }
}

compileJava.options.sourcepath = files('src/main/resources')

group 'io.graphoenix'
version '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile project(':graphoenix-spi')
    compile 'org.antlr:antlr4-runtime:4.9.3'
    compile 'com.google.guava:guava:31.1-jre'
    compile 'io.vavr:vavr:0.10.4'
    compile 'com.google.auto.service:auto-service:1.0.1'
    compile 'org.eclipse.microprofile.reactive-streams-operators:microprofile-reactive-streams-operators-core:2.0'
    compile 'com.lightbend.microprofile.reactive.streams:lightbend-microprofile-reactive-streams-zerodep:1.0.0'
    compile 'com.aventrix.jnanoid:jnanoid:2.0.0'
    compile 'com.github.ben-manes.caffeine:caffeine:3.1.1'

    implementation project(':graphoenix-config')
    implementation project(':graphoenix-json')
    implementation 'org.antlr:ST4:4.3.2'
    implementation 'com.networknt:json-schema-validator:1.0.70'
    implementation 'com.jcabi:jcabi-manifests:1.2.1'
    implementation 'org.tinylog:tinylog-impl:2.4.1'
    implementation 'org.tinylog:slf4j-tinylog:2.4.1'
    implementation 'org.tinylog:jcl-tinylog:2.4.1'

    runtimeOnly 'io.grpc:grpc-netty-shaded:1.52.1'
    implementation 'io.grpc:grpc-protobuf:1.52.1'
    implementation 'io.grpc:grpc-stub:1.52.1'
    implementation 'com.salesforce.servicelibs:reactor-grpc-stub:1.2.3'

    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+

    annotationProcessor project(':graphoenix-spi')
    annotationProcessor project(':graphoenix-inject')
    annotationProcessor project(':graphoenix-interceptor')
    annotationProcessor project(':graphoenix-config')
    annotationProcessor project(':graphoenix-json')
    annotationProcessor 'com.networknt:json-schema-validator:1.0.70'

    testImplementation 'io.projectreactor:reactor-test:3.4.16'
    testRuntimeOnly 'io.projectreactor:reactor-test:3.4.16'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                "Graphoenix-Version": project.version
        )
    }
}